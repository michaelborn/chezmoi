default:
    just --list

setup: install-basics install-apps dark-mode install-starship install-ulauncher install-librewolf

setup-work: && setup install-work-apps
    echo "Running work-specific setup"
    just install-commandbox
    just install-java
    just install-nordlayer

# Curl, git, vim, etc.
install-basics:
    sudo apt install -y curl git vim
    sudo apt install wmctrl # Wayland hotkey fix - https://github.com/Ulauncher/Ulauncher/wiki/Hotkey-In-Wayland
    sudo apt install ddcutil # display brightness control util

# Work-specific apps like postman, zoom, dbeaver, ngrok, and NM VPN tooling
install-work-apps:
    sudo apt install flameshot network-manager-openconnect-gnome
    sudo snap install slack pomotroid postman dbeaver-ce zoom-client ngrok chromium
    just install-pipewire

# Install common apps like chezmoi, code, starship, etc.
install-apps:
    sudo snap install chezmoi --classic
    sudo snap install code --classic
    just install-docker

# See https://dashboard.ngrok.com/get-started/your-authtoken
# Add your ngrok auth token to local ngrok config
authenticate-ngrok token:
    ngrok config add-authtoken '{{token}}'

# Install starship and optionally add to .bashrc
install-starship:
    curl -sS https://starship.rs/install.sh | sh
    # Should be unnecessary as .bashrc is managed by chezmoi
    # echo 'eval "$(starship init bash)"' >> ~/.bashrc

# Set gnome dark mode
dark-mode:
    gsettings set org.gnome.desktop.interface gtk-theme 'Adwaita-dark'

# Set gnome light mode
light-mode:
    gsettings set org.gnome.desktop.interface gtk-theme 'Adwaita'

# Common gnome extensions - Tiling shell, mic muter, fullscreen notifs
install-gnome-extensions:
    sudo apt install gnome-browser-connector
    # https://extensions.gnome.org/extension/1610/fullscreen-notifications/
    # https://extensions.gnome.org/extension/1113/nothing-to-say/
    # https://extensions.gnome.org/extension/7065/tiling-shell/ 

# https://help.preyproject.com/article/317-unattended-install-ubuntu
# Ortus Prey inventory tracker
install-prey $API_KEY PREY_VERSION:
    echo $API_KEY
    curl https://downloads.preyproject.com/prey-client-releases/node-client/{{ PREY_VERSION }}/prey_{{ PREY_VERSION }}_amd64.deb --output Downloads/prey_{{ PREY_VERSION }}_amd64.deb
    sudo apt install libgtk-3-dev scrot streamer mpg123
    # sudo apt --fix-broken install
    # sudo apt-get -f install
    sudo -E dpkg -i Downloads/prey_{{ PREY_VERSION }}_amd64.deb

setup-prey PREY_SETUP_KEY:
    prey config account authorize -a {{ PREY_SETUP_KEY}}

install-ulauncher:
    sudo add-apt-repository universe -y
    sudo add-apt-repository ppa:agornostal/ulauncher -y
    sudo apt update
    sudo apt install ulauncher

# Flatpak, for installing Waterfox and Zen browser
install-flatpak:
    sudo apt install flatpak
    sudo apt install gnome-software-plugin-flatpak
    flatpak remote-add --if-not-exists flathub https://dl.flathub.org/repo/flathub.flatpakrepo

# Waterfox browser
install-waterfox:
    flatpak install flathub net.waterfox.waterfox

# Zen browser
install-zen:
    flatpak install flathub app.zen_browser.zen

# Librewolf browser
install-librewolf:
    sudo apt update && sudo apt install extrepo -y
    sudo extrepo enable librewolf
    sudo apt update && sudo apt install librewolf -y

# Install CommandBox
install-commandbox:
    mkdir -p ~/bin
    wget https://www.ortussolutions.com/parent/download/commandbox/type/linux-jre64 -O ~/bin/box.zip
    unzip ~/bin/box.zip -d ~/bin
    rm ~/bin/box.zip

# Install java
install-java:
   sudo apt install openjdk-21-jdk

install-docker:
    sudo snap install docker
    sudo groupadd docker
    sudo usermod -aG docker $USER
    newgrp docker

# https://www.baeldung.com/linux/pulseaudio-pipewire-replace
# Pipewire and pipewire utils necessary for fixing zoom screen capture in Wayland/Gnome 49.
install-pipewire:
    sudo apt install -y pipewire pipewire-audio-client-libraries pulseaudio-utils

install-nordlayer:
  curl https://downloads.nordlayer.com/linux/latest/debian/latest/nordlayer_latest_amd64.deb --output ~/Downloads/nordlayer_latest_amd64.deb
  sudo dpkg -i ~/Downloads/nordlayer_latest_amd64.deb

# Disable screen lock and notifications
meeting-start:
  gsettings set org.gnome.desktop.lockdown disable-lock-screen true
  gsettings set org.gnome.desktop.notifications show-banners false

# Re-enable screen lock and notifications
meeting-end:
  gsettings set org.gnome.desktop.lockdown disable-lock-screen false
  gsettings set org.gnome.desktop.notifications show-banners true
